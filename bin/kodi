#!/usr/bin/perl
use strict;
use warnings;
use Time::HiRes qw(sleep);

my $DELAY_KEYS_MS = 1000;
my $DELAY_CMD_MS = 3000;

sub run(@);

my $usage = "Usage:
  $0 -h|--help
    print this message

  $0 FILE
    -run `kodi`
    -run `keys-to-window kodi`
    -run `kodi-cmd open FILE`

  $0 FILE POSITION
    -run `kodi`
    -run `keys-to-window kodi`
    -run `kodi-cmd open FILE`
    -run `kodi-cmd seek POSITION`

  $0 [ARG ARG ..]
    -run `kodi ARG ARG ..`
    -run `keys-to-window kodi`
";

sub main(@){
  die $usage if @_ == 1 and $_[0] =~ /^(-h|--help)$/;

  my @kodiCmds;
  my @kodiArgs;
  if(@_ == 1 and -f $_[0]){
    push @kodiCmds, ["open", $_[0]];
    @kodiArgs = ();
  }elsif(@_ == 2 and -f $_[0] and $_[1] =~ /^(\d+|\d+:\d+|\d+:\d+:\d+|\d+%)$/){
    push @kodiCmds, ["open", $_[0]];
    push @kodiCmds, ["seek", $_[1]];
    @kodiArgs = ();
  }else{
    @kodiArgs = @_;
  }

  my $keysPid = fork;
  if($keysPid == 0){
    sleep($DELAY_KEYS_MS / 1000.0);
    exec "keys-to-window", "kodi";
    exit 0;
  }

  my $cmdPid = fork;
  if($cmdPid == 0){
    sleep($DELAY_CMD_MS / 1000.0);
    for my $cmd(@kodiCmds){
      run "kodi-cmd", @$cmd;
    }
    exit 0;
  }

  run "/usr/bin/kodi", @kodiArgs;
  run "kill", $keysPid;
  run "kill", $cmdPid;
  run "stty", "sane";
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
