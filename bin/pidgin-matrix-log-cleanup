#!/usr/bin/perl
use strict;
use warnings;

my $PURPLE_DIR = "$ENV{HOME}/.purple";

sub readLines($);
sub run(@);

sub main(@){
  archiveUselessLogs();
}

sub archiveUselessLogs(){
  my @chatDirs = grep {-d $_ and not -l $_} glob "$PURPLE_DIR/logs/matrix/*/*";
  for my $chatDir(sort @chatDirs){
    my @logFiles = glob("$chatDir/*.txt");
    for my $logFile(sort @logFiles){
      my @lines = readLines $logFile;
      my $reUser = qr/[a-zA-Z0-9:!\.]+/;
      my $reDate = qr/[a-zA-Z0-9: ]+/;
      my $reAcc = qr/\w+/;
      my $reTimestamp = qr/\(\d\d:\d\d:\d\d(?: [AP]M)?\)/;
      my $reDisconnectedMsg = ""
        . qr/The account has disconnected and you are no longer in this chat\./
        . qr/ You will automatically rejoin the chat when the account reconnects\./
        ;

      my $useless = 1;
      for my $line(@lines){
        if($line =~ /^Conversation with $reUser at $reDate on $reAcc \(matrix\)$/){
          next;
        }elsif($line =~ /^$reTimestamp $reDisconnectedMsg$/){
          next;
        }elsif($line =~ /^$reTimestamp $reUser (?:entered|left) the room\.$/){
          next;
        }else{
          $useless = 0;
          last;
        }
      }

      if($useless){
        my $archiveDir = "$chatDir/archive";
        if(not -d $archiveDir){
          run "mkdir", "-p", $archiveDir
        }
        die "could not mkdir $archiveDir\n" if not -d $archiveDir;
        run "mv", "--no-clobber", $logFile, "$archiveDir/";
      }
    }
  }
}

sub readLines($){
  my ($file) = @_;
  open RLFH, "< $file" or die "could not read $file\n$!\n";
  my @lines = <RLFH>;
  close RLFH;
  return @lines;
}

sub run(@){
  print "@_\n";
  system @_;
}
&main(@ARGV);
