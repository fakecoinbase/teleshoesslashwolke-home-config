#!/usr/bin/perl
use strict;
use warnings;

my ($MODE_PSTATE, $MODE_CPUFREQ) = ("PSTATE", "CPUFREQ");

my $PSTATE_DEV = "/sys/devices/system/cpu/intel_pstate/max_perf_pct";
my $CPUFREQ_DEV = "/sys/devices/system/cpu/cpu0/cpufreq/scaling_max_freq";

my $CPUFREQ_CPUINFO_MIN_DEV = "/sys/devices/system/cpu/cpu0/cpufreq/cpuinfo_min_freq";
my $CPUFREQ_CPUINFO_MAX_DEV = "/sys/devices/system/cpu/cpu0/cpufreq/cpuinfo_max_freq";
my $CPUFREQ_SCALING_MAX_DEV = "/sys/devices/system/cpu/cpu0/cpufreq/scaling_max_freq";

my $usage = "Usage:
  $0 -h|--help
    print this message

  $0
  $0 -g|--get
    print max cpu

    if $PSTATE_DEV exists:
      cpu-set-pstate -g max
    if $CPUFREQ_DEV exists:
      cat $CPUFREQ_DEV
    otherwise fail

  $0 PCT
    PCT is a non-negative integer, optionally followed by '%'
    if $PSTATE_DEV exists:
      cpu-set-pstate 0% <PCT>%
    if $CPUFREQ_DEV exists:
      cpu-set-cpufreq all ondemand 0% <PCT>%
";

sub main(@){
  die $usage if @_ > 0 and $_[0] =~ /^(-h|--help)$/;

  my $mode = undef;
  if(-e $PSTATE_DEV){
    $mode = $MODE_PSTATE;
  }elsif(-e $CPUFREQ_DEV){
    $mode = $MODE_CPUFREQ;
  }else{
    die "could not find pstate or cpufreq devices:\n$PSTATE_DEV\n$CPUFREQ_DEV\n";
  }

  if(@_ == 0 or $_[0] =~ /^(-g|--get)$/){
    if($mode eq $MODE_PSTATE){
      exec "cpu-set-pstate", "-g", "max";
    }elsif($mode eq $MODE_CPUFREQ){
      my $cpuinfoMin = `cat $CPUFREQ_CPUINFO_MIN_DEV`;
      my $cpuinfoMax = `cat $CPUFREQ_CPUINFO_MAX_DEV`;
      my $scalingMax = `cat $CPUFREQ_SCALING_MAX_DEV`;
      chomp $cpuinfoMin;
      chomp $cpuinfoMax;
      chomp $scalingMax;
      my $cur = $scalingMax - $cpuinfoMin;
      my $max = $cpuinfoMax - $cpuinfoMin;
      printf "%d%%\n", $cur/$max*100.0;
    }
  }elsif(@_ == 1 and $_[0] =~ /^(\d+)%?$/){
    my $pct = $1;
    if($mode eq $MODE_PSTATE){
      exec "cpu-set-pstate", "0%", "$pct%";
    }elsif($mode eq $MODE_CPUFREQ){
      exec "cpu-set-cpufreq", "all", "ondemand", "0%", "$pct%";
    }
  }
}

&main(@ARGV);
