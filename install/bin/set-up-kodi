#!/usr/bin/perl
use strict;
use warnings;
use lib `dirname $0 | tr -d '\n'`;
use ScriptScript;

my $kodiDir = getHome() . "/.kodi";

my $pulse = 1;

my $card = $pulse ? "PULSE:Default" : "ALSA:hdmi:CARD=HDMI,DEV=0";

my $addonsDb = "$kodiDir/userdata/Database/Addons27.db";
my $guisettingsFile = "$kodiDir/userdata/guisettings.xml";

my $bgDir = getHome() . "/.cache/backgrounds/fhd";

my $skin = {
  "skin.confluence.UseCustomBackground" => "true",
  "skin.confluence.CustomBackgroundPath" => "$bgDir/ghibli/nausicaa.jpg",
};

my $guisettings = {
  showhidden => "true",
  audiodevice => $card,
  channels => $pulse ? 1 : 8, #2.0 or 5.1
  passthrough => $pulse ? "false" : "true",
  passthroughdevice => $pulse ? "default" : $card,
  streamsilence => "-1", #keep audio device alive
  enablerssfeeds => "false",
};

my @disabledAddonIds = qw(
  service.xbmc.versioncheck
  metadata.musicvideos.theaudiodb.com
);

sub main(@) {
    getRoot @_;

    for my $addonId(@disabledAddonIds){
      my $intId = `sqlite3 $addonsDb "select id from addons where addonId='$addonId'"`;
      chomp $intId;
      die "error getting integer id for addonId=$addonId\n" if $intId !~ /^\d+$/;
      print "disabling $addonId(id=$intId) in addons database\n";
      system "sqlite3", $addonsDb, "
        delete from blacklist where id=$intId;
        insert into blacklist(id, addonID) values($intId, '$addonId');
      ";
    }

    editFile $guisettingsFile, sub {
      my $c = shift;
      for my $key(sort keys %$skin){
        my $val = $$skin{$key};
        my $type = $val =~ /^(true|false)$/ ? "bool" : "string";

        my $typeAtt = "type=\"$type\"";
        my $nameAtt = "name=\"$key\"";

        my $prefix = "\\s*<setting\\s*$typeAtt\\s*$nameAtt>";
        my $line = "        <setting $typeAtt $nameAtt>$val</setting>";

        if(not replaceLine $c, $prefix, $line){
          die "could not update skin setting $key = $val\n";
        }
      }
      for my $key(sort keys %$guisettings){
        my $val = $$guisettings{$key};
        my $prefix = "\\s*<$key( [^>]*)?";
        my $line = "        <$key>$val</$key>";
        if(not replaceLine $c, $prefix, $line){
          die "could not update gui setting $key = $val\n";
        }
      }
      $c;
    };
}

&main(@ARGV);
